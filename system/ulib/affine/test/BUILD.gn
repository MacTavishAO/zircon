# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxb/36139.                         #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/test.gni")
import("//build/test/test_package.gni")

import("//build/fuzzing/fuzzer.gni")

group("test") {
  testonly = true
  deps = [
    ":affine",
    ":affine-fuzzer",
  ]
}

test("affine") {
  output_name = "affine-test"
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [
    "ratio.cc",
    "transform.cc",
    "utils.cc",
  ]
  deps = [
    "//sdk/lib/fdio",
    "//zircon/public/lib/affine",
    "//zircon/public/lib/fit",
    "//zircon/public/lib/zxtest",
  ]

  # TODO(41888): UBSan has found an instance of undefined behavior in this target.
  # Disable UBSan for this target temporarily until it is migrated into CI/CQ.
  configs += [ "//build/config:temporarily_disable_ubsan_do_not_use" ]
}

fuzzer("affine-fuzzer") {
  sources = [ "affine-fuzzer.cc" ]
  deps = [ "//zircon/public/lib/affine" ]
}

unittest_package("affine-package") {
  package_name = "affine"
  deps = [ ":affine" ]

  tests = [
    {
      name = "affine-test"
    },
  ]
}
