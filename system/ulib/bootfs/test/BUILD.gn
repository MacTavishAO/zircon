# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxb/36139.                         #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/test.gni")
import("//build/test/test_package.gni")

import("//build/fuzzing/fuzzer.gni")

group("test") {
  testonly = true
  deps = [
    ":bootfs-parser-fuzzer",
    ":bootfs-test",
  ]
}

test("bootfs-test") {
  output_name = "bootfs-test-test"
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [ "parser-test.cc" ]
  deps = [
    "//sdk/lib/fdio",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/bootfs",
  ]
}

fuzzer("bootfs-parser-fuzzer") {
  sources = [ "parser-fuzzer.cc" ]
  deps = [
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/bootfs",
  ]
}

unittest_package("bootfs-test-package") {
  package_name = "bootfs-test"
  deps = [ ":bootfs-test" ]

  tests = [
    {
      name = "bootfs-test-test"
    },
  ]
}
