# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxb/36139.                         #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/fidl/fidl.gni")
import("//build/test.gni")
import("//build/test/test_package.gni")

test("fidl-utils") {
  output_name = "fidl-utils-test"
  if (is_fuchsia) {
    configs += [ "//build/unification/config:zircon-migrated" ]
  }
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [ "binding_tests.cc" ]
  deps = [
    ":fidl.test.fidlutils_c",
    "//sdk/lib/fdio",
    "//zircon/system/ulib/fidl-utils",
    "//zircon/system/ulib/unittest",
  ]
}

fidl("fidl.test.fidlutils") {
  visibility = [ ":*" ]
  sources = [ "binding_tests.fidl" ]
}

unittest_package("fidl-utils-package") {
  package_name = "fidl-utils"
  deps = [ ":fidl-utils" ]

  tests = [
    {
      name = "fidl-utils-test"
    },
  ]
}
